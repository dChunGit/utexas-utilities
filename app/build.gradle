
apply plugin: 'com.android.application'

// Some constant information you could use in builds, accessible via BuildConfig.<field-name>
//def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.nasageek.utexasutilities"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 46
        versionName "2.0.1b"

        // The git sha of the current head and build time
        //buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
        javaMaxHeapSize "3584m"
    }

    // TODO: You can put sensitive keystore data in an untracked .properties file or ENV variables
    // Example: http://stackoverflow.com/a/20872275/3034339
    signingConfigs {
        release {
            storeFile file('keystore-file-here')
            storePassword ''
            keyAlias ''
            keyPassword ''
        }
    }
    buildTypes {
        debug {
            // Can add package and versionName suffixes, makes it easier to differentiate if you have play store and debug builds installed
            debuggable true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
        }
        // Release build task. Uses the release signing config above for signing it
        // To run from command line, use './gradlew.bat assembleRelease' from project root
        release {
            debuggable false
            minifyEnabled true
            useProguard true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    debugImplementation 'com.squareup.okhttp:mockwebserver:2.7.5'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    implementation 'com.google.maps.android:android-maps-utils:0.4.4'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.commonsware.cwac:pager:0.2.6'
    implementation 'com.google.android.gms:play-services-analytics:15.0.2'
    implementation 'com.google.android.gms:play-services-base:15.0.1'
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    implementation 'com.madgag.spongycastle:pkix:1.54.0.0'
    implementation 'com.commonsware.cwac:security:0.8.0'
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'
    implementation 'de.cketti.library.changelog:ckchangelog:1.2.2'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.0.1'
    implementation 'com.squareup:otto:1.3.8'
    implementation('ch.acra:acra:4.9.2') {
        exclude group: 'org.json'
    }
    implementation files('src/main/libs/amazinglistview.jar')
}

repositories {
    maven { url 'http://dl.bintray.com/populov/maven' }
    maven { url 'https://s3.amazonaws.com/repo.commonsware.com' }
    maven { url "https://jitpack.io" }
}

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:3.3.0'
    }
}

apply plugin: 'com.google.gms.google-services'
